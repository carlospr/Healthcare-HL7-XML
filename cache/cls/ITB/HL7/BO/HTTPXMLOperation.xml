<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="25">
<Class name="ITB.HL7.BO.HTTPXMLOperation">
<Description>
Business Operation that receives an EnsLib.HL7.Message and sends it as HL7 (XML) 
over HTTP returning received ACK as EnsLib.HL7.Message</Description>
<Super>ITB.HL7.BO.XMLOperation</Super>
<TimeCreated>63242,40692.99119</TimeCreated>

<Parameter name="SrcVer">
<Description>
Location and Revision of this file in Perforce (Auto-updating)</Description>
<Default>$Id$</Default>
</Parameter>

<Parameter name="ADAPTER">
<Default>EnsLib.HTTP.OutboundAdapter</Default>
</Parameter>

<Property name="Adapter">
<Type>EnsLib.HTTP.OutboundAdapter</Type>
</Property>

<Parameter name="INVOCATION">
<Default>Queue</Default>
</Parameter>

<Property name="ContentType">
<Description>
Content Type used to send the HTTP content</Description>
<Type>%String</Type>
<InitialExpression>"application/hl7-v2+xml"</InitialExpression>
</Property>

<Parameter name="SETTINGS">
<Default>ContentType,SearchTableClass::selector?context={Ens.ContextSearch/SearchTableClasses?host=EnsLib.HL7.Service.Standard},MessageSchemaCategory:Basic:selector?context={Ens.ContextSearch/SchemaCategories?host=EnsLib.HL7.Service.Standard},AlertGracePeriod:Alerting</Default>
</Parameter>

<Method name="SendHL7">
<Description>
Send EnsLib.HL7.Message</Description>
<FormalSpec>pRequest:EnsLib.HL7.Message,*pResponse:EnsLib.HL7.Message</FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
	set ret = $$$OK
	
	try {
		$$$THROWONERROR(tSC, ..SendHL7XML(pRequest, .pResponse, 1))
	} catch ex {
		set ret = ex.AsStatus()
	}
	
	quit ret
]]></Implementation>
</Method>

<Method name="AdapterSendStream">
<Description>
Send stream using Adapter (this method should be overwritten in derived operations)</Description>
<FormalSpec>pRequest:%Stream.Object,*pResponse:%Stream.Object</FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
	set ret = $$$OK
	try {
		set tHttpRequest=##class(%Net.HttpRequest).%New()
		set tHttpRequest.WriteRawMode=1
		set tHttpRequest.ContentType = ..ContentType
		do tHttpRequest.EntityBody.CopyFrom(pRequest)
		set tHttpRequest.ResponseStream=##class(%IO.StringStream).%New()
		
		$$$THROWONERROR(tSC,..Adapter.SendFormDataArray(.tHttpResponse, "Post", tHttpRequest))
		$$$TRACE("Received "_tHttpResponse.Data.Size_" bytes")
		if tHttpResponse.Data.Size {
			set pResponse = ##class(%GlobalCharacterStream).%New()
			do pResponse.CopyFrom(tHttpResponse.Data)
		}
	} catch ex {
		set ret = ex.AsStatus()
	}
	
	quit ret
]]></Implementation>
</Method>

<XData name="MessageMap">
<Data><![CDATA[
<MapItems>
	<MapItem MessageType="EnsLib.HL7.Message"> 
		<Method>SendHL7</Method>
	</MapItem>
</MapItems>
]]></Data>
</XData>
</Class>
</Export>
